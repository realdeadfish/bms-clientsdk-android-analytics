/*
    Copyright 2015 IBM Corp.
    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at
        http://www.apache.org/licenses/LICENSE-2.0
    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.
*/

apply plugin: 'com.android.library'

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.kt3k.coveralls'

apply from: 'jacoco.gradle'

ext {
    ANALYTICS_VERSION_VAR_NAME = 'BMS_ANALYTICS_SDK_VERSION'
    ANALYTICS_SDK_VERSION = '1.2.3-SNAPSHOT'

}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
            include '**/*Test.class'
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile 'com.ibm.mobilefirstplatform.clientsdk.android:core:3.1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-httpclient:3.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.9.0'
    testCompile 'org.json:json:20160810'
    compile 'com.google.android.gms:play-services-location:10.0.1'
    compile 'com.android.support:design:24.0.0'
}

task createPropertiesFile() {
    def prop = new Properties()
    def propFile = new File("${project.projectDir}/src/main/resources/sdk.properties");

    if (propFile.getParentFile() != null && !propFile.getParentFile().exists()) {
        propFile.getParentFile().mkdirs()
    }

    propFile.createNewFile();
    prop.setProperty('sdk.version', ANALYTICS_SDK_VERSION)
    prop.store(propFile.newWriter(), null);
}

assembleDebug.dependsOn createPropertiesFile
assembleRelease.dependsOn createPropertiesFile

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/internal/**'

    options.links 'http://docs.oracle.com/javase/7/docs/api/'

    options.encoding 'UTF-8'

    options.memberLevel = JavadocMemberLevel.PUBLIC

    setTitle 'Bluemix Mobile Services Analytics Android API'

    failOnError = false
}

task androidJavadocsJar(type: Jar, dependsOn: generateJavadoc) {
    classifier = 'javadoc'
    from generateJavadoc.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task moveClassesToJarFolder(type: Copy, dependsOn: assembleRelease) {
    from 'build/intermediates/classes/release/'
    into 'build/intermediates/jars/'
    exclude '**/BuildConfig.*'
}

task moveResourcesToJarFolder(type: Copy, dependsOn: assembleRelease) {
    from 'src/main/resources/'
    into 'build/intermediates/jars/'
    exclude '**/BuildConfig.*'
}

task androidLibraryJar(type: Jar, dependsOn: [moveClassesToJarFolder, moveResourcesToJarFolder]) {
    from 'build/intermediates/jars/'
    archiveName project.name + '.jar'
    exclude '**/BuildConfig.*'
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
    archives androidLibraryJar
}

signing {
    required { isReleaseBuild() }
    sign configurations.archives
}

def getRepositoryUsername() {
    return hasProperty('ossrhUsername') ? ossrhUsername : ''
}

def getRepositoryPassword() {
    return hasProperty('ossrhPassword') ? ossrhPassword : ''
}

def getReleaseRepositoryUrl() {
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}

def getSnapshotRepositoryUrl() {
    return 'https://oss.sonatype.org/content/repositories/snapshots/'
}

def isReleaseBuild() {
    return !getVersion().contains('-SNAPSHOT')
}

def getVersion() {
    return ANALYTICS_SDK_VERSION
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getReleaseRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            snapshotRepository(url: getSnapshotRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.groupId = 'com.ibm.mobilefirstplatform.clientsdk.android'
            pom.artifactId = 'analytics'
            pom.version = getVersion()

            pom.project {
                name 'IBM Bluemix Mobile Services - Client SDK Android Analytics'
                description 'This is the analytics component of the Android SDK for IBM Bluemix Mobile Services.'
                url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-analytics'

                scm {
                    connection 'scm:git:git@github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-analytics.git'
                    developerConnection 'scm:git:git@github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-analytics.git'
                    url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-analytics'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'IBM Bluemix Services Mobile SDK'
                        email 'mobilsdk@us.ibm.com'
                    }
                }
            }
        }
    }
}
